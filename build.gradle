apply plugin: 'idea'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'


group = 'io.innerloop'
version = '0.1'


dependencies {
    compile gradleApi()
    groovy localGroovy()
    testCompile 'junit:junit:4.11'
}


repositories {
    mavenCentral()
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.5'
}

task sourcesJar(type: Jar, dependsOn:classes) {
     classifier = 'sources'
     from sourceSets.main.allSource
}


task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'groovydoc'
    from groovydoc.destinationDir
}


artifacts {
     archives sourcesJar
     archives groovydocJar
}


signing {
     sign configurations.archives
}

 
uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            repository(url: "http://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: System.getProperty('nexusUsername'), password: System.getProperty('nexusPassword'))
      }
            pom.project {
                name project.name
                description "Gradle plugin for using Google Protocol Buffers in your projects across Java, Python and C++"
                url "https://github.com/inner-loop/gradle-protobuf-plugin"
                licenses {
                    license {
                        name "The MIT License (MIT)"
                        url "https://github.com/inner-loop/gradle-protobuf-plugin/blob/master/LICENSE"
                    }
                }
                developers {
                    developer {
                        id "mangrish"
                        name "Mark Angrish"
                        email "mark.angrish@innerloop.io"
                    }
                }
                scm {
                    connection "scm:git:git://github.com/inner-loop/gradle-protobuf-plugin.git"
                    developerConnection "scm:git:git@github.com:inner-loop/gradle-protobuf-plugin.git"
                    url "https://github.com/inner-loop/gradle-protobuf-plugin"
                }
            }
        }
    }
}
